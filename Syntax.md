HDML 语法说明
========================

**注**：`HDML` 只是一种语言规范，并不做实际的文档展示约束，
但会针对普遍的情形提供一些建议和参考。

HDML 将一个文件视为一个**文档**（`Document`）。

文档由不同的**块**（`Block`）组成，在一个**块**内可嵌套其他**块**，
在块内也可以链接其他块，也可以直接引用*块的内容*。

一个**文档**在本质上即为一个块--**超级块**（`Super Block`），
在该块内嵌入了文档**内容块**，在数据结构上构成一颗倒置的树。

## 属性标记符

包括文档在内，所有的块均可设置属性，用于声明块的基础信息和内容布局等。

块的属性通过**属性标记符**指示，其语法结构为：
```
@<attr-name> <attr-value>
```

- 多级属性
```
@doc
@.author
@..name 张三
@..email zhangsan@example.com
```

每多一个层级，则在属性名称前便多一个`.`。

- 文档属性
```
@doc.title 三天速成？不存在的！
@include ./a/b/c.hdoc
```

属性名称前不带`.`的为文档属性，文档属性可以出现在文档的任意位置，
但一般集中在文档的开头。

最终，文档属性及其子级属性将合并在一起，同层级的同名属性将做覆盖处理，
即，后一个的值覆盖前一个。

- 多行值属性
```
@doc.title
三天速成？不存在的！

    -- 评《Xxx》
@$
```

- 常量属性
```
@author 张三 <zhangsan@example.com>
```

引用方式为`@@author`，比如：
```
@doc.author @@author
```

在行内的引用方式为`{{@@author}}`：
```
本文作者为 {{@@author}}。
```

- Boolean 属性
```
@.collapsed? true|false

@.collapsed? # == true
```

## 文档

```
@doc
@.title
三天速成？不存在的！

    -- 评《Xxx》
@$
@.author
@..name 张三
@..email zhangsan@example.com

文档正文。。。

-----
@.level 1
@.title 第 1 节

这是第 1 节。。。

-----
@.level 1
@.title 第 2 节

这是第 2 节。。。
```

## 块

块由**块起止符**标识其内容的边界，其**起始符**为 `{{|`，而**结束符**为 `}}`。
一般要求在块起止符之间保留两个空行，块的内容从两个空行之间开始书写。

块分为以下两种：
- 匿名块
```
{{|

这是匿名块。

}}
```
- 命名块
```
{{SomeName|

这是命名块。

}}
```

命名块是有特定的*渲染处理模块*的，通过名称可以统一限定对块内容的渲染方式，
同时也能更加明确块在文档中的职能，比如，索引目录块、引用文献块、代码块等。

*块的属性信息*紧挨着**块起始符**，并与块内容间隔一个空行：
```
{{Toc|
@.numbered?

@include ./a.hdoc
@include ./b.hdoc

}}
```

### 段落

```
这段文本实际上也是一个块。
```

每一个文本**段落**（`Paragraph`）均为一个块，但其不需要包裹在**块起止符**内，
仅需段落之间，段落与其他块之间间隔一个或多个空白行即可。以上实例等价于：
```
{{Paragraph|

这段文本实际上也是一个块。

}}
```

由于段落也是块，所以，可以为段落设置属性信息：
```
@.indent 4
这段文本将缩进4个字符。
```

注：段落属性需紧挨者段落，二者之间没有空行。

### 章节

**章节**（`Section`）以至少三个`-`作为分隔符，
在遇到下一个**章节分隔符**之前的内容均属于当前章节。

```
-----
@.level 1
@.title 第 1 节

这是第 1 节。。。

-----
@.level 2
@.title 第 1.1 节

这是第 1.1 节。。。

-----
@.level 3
@.title 第 1.1.1 节

这是第 1.1.1 节。。。

-----
@.level 1
@.title 第 2 节

这是第 2 节。。。
```

### 块嵌套

```
{{Translation|
@.layout row

-----
@.lang en
@.title English
@.author 张三

This is a English paragraph.

-----
@.lang zh
@.title 中文
@.author 李四

这是一段英文。

}}
```

### 代码块

<pre>
```
@.lang java

int a = 0;
```
</pre>

等价于

```
{{Source|
@.lang java

int a = 0;

}}
```

## 内联样式

- 加粗
```
**粗体**
```

- 样式属性
```
**粗体**{@.style.color red, @.style.font.size 24px}
```

- 无样式
<pre>
`只是为了限定该小段文本的边界`
</pre>

- 引用样式
```
@bigRedFont
@.color red
@.font.size 24px

**大红字体**{@.style @@bigRedFont}
```
